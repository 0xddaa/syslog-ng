/*
 * Copyright (c) 2002-2013 BalaBit IT Ltd, Budapest, Hungary
 * Copyright (c) 1998-2013 Bal√°zs Scheidler
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms of the GNU General Public License version 2 as published
 * by the Free Software Foundation, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

%code top {
#include "dbparser-parser.h"

}


%code {

#include "dbparser.h"
#include "cfg-grammar.h"
#include "correllate-parser.h"
#include "cfg-parser.h"
#include "dbparser-grammar.h"
#include "syslog-names.h"
#include "messages.h"
#include <string.h>

SyntheticMessage *last_message;
}

%name-prefix "dbparser_"

/* this parameter is needed in order to instruct bison to use a complete
 * argument list for yylex/yyerror */

%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {LogParser **instance}
%parse-param {gpointer arg}

/* INCLUDE_DECLS */

%token KW_DB_PARSER
%token KW_CORRELLATE
%token KW_INJECT_MODE
%token KW_CONTEXT_ID
%token KW_INHERIT_PROPERTIES
%token KW_TIMEOUT

%type <cptr> stateful_parser_inject_mode
%type <ptr>  synthetic_message

%%

start
        : LL_CONTEXT_PARSER parser_expr_db                  { YYACCEPT; }
        ;

parser_expr_db
        : KW_DB_PARSER '('
          {
            last_parser = *instance = (LogParser *) log_db_parser_new(configuration);
          }
          parser_db_opts
          ')'
	| KW_CORRELLATE '('
	  {
	    last_parser = *instance = correllate_new(configuration);
	  }
          correllate_opts ')'
        ;

parser_db_opts
        : parser_db_opt parser_db_opts
        |
        ;

/* NOTE: we don't support parser_opt as we don't want the user to specify a template */
parser_db_opt
        : KW_FILE '(' string ')'                		{ log_db_parser_set_db_file(((LogDBParser *) last_parser), $3); free($3); }
	| stateful_parser_opt
        ;

stateful_parser_opt
	: KW_INJECT_MODE '(' stateful_parser_inject_mode ')'	{ stateful_parser_set_inject_mode(((StatefulParser *) last_parser), $3); free($3); }
	| parser_opt
	;

stateful_parser_inject_mode
	: string				{ $$ = $1; }
	| KW_INTERNAL				{ $$ = strdup("internal"); }
	;

correllate_opts
	: correllate_opt correllate_opts
	|
	;

correllate_opt
	: KW_CONTEXT_ID '(' template_content ')'		{ correllate_set_context_id_template(last_parser, $3); log_template_unref($3); }
	| KW_TIMEOUT '(' LL_NUMBER ')'				{ correllate_set_context_timeout(last_parser, $3); }
	| KW_MESSAGE '(' synthetic_message ')'			{ correllate_set_synthetic_message(last_parser, $3); }
	| stateful_parser_opt
	;

synthetic_message
	: { last_message = synthetic_message_new(); } synthetic_message_opts { $$ = last_message; }
	;


synthetic_message_opts
	: synthetic_message_opt synthetic_message_opts
	|
	;

synthetic_message_opt
	: KW_INHERIT_PROPERTIES '(' string ')'			{ correllate_set_inherit_properties(last_parser, $3); free($3); }
	| KW_VALUE '(' string template_content ')'
	  {
	    synthetic_message_add_value_template(last_message, $3, $4);
	    free($3);
            log_template_unref($4);
	  }
	| KW_TAGS '(' string ')'				{ synthetic_message_add_tag(last_message, $3); free($3); }
	;


/* INCLUDE_RULES */

%%

